package org.drools.examples

import org.drools.dotnet.examples

rule "find solution"
    when

        Golfer( fredsName : Name == "Fred", 
                fredsPosition : Position, 
                fredsColor : Color  )

  		
        Golfer( unknownsName : Name != "Fred", 
                unknownsPosition :  Position == (fredsPosition + 1) ,
                unknownsColor : Color == "blue",
                Color != fredsColor )

        
        Golfer( joesName : Name == "Joe", 
                joesPosition  : Position == 2, 
                Position != fredsPosition,
                joesColor : Color != fredsColor )

 		
        Golfer( bobsName : Name == "Bob", 
                Name != unknownsName,
                bobsPosition : Position != fredsPosition,
                Position != unknownsPosition,              
                Position != joesPosition,                                                  
                bobsColor : Color == "plaid",
                Color != fredsColor,
                Color != joesColor,
                Color != unknownsColor )
 		  		
        Golfer( tomsName : Name == "Tom", 
                tomsPosition : Position != 1,
                Position != 4,
                Position != fredsPosition,
                Position != joesPosition, 
                Position != bobsPosition,                                
                tomsColor : Color != "orange",
                Color != "blue",
                Color != fredsColor,
                Color != joesColor,
                Color != bobsColor )                
	then
        System.Console.Out.WriteLine( "Fred " + fredsPosition + " " + fredsColor );
        System.Console.Out.WriteLine( "Joe " + joesPosition + " " + joesColor );
        System.Console.Out.WriteLine( "Bob " + bobsPosition + " " + bobsColor );
        System.Console.Out.WriteLine( "Tom " + tomsPosition + " " + tomsColor );   
end    
