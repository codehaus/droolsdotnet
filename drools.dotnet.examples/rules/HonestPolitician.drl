package org.drools.examples;
 
import org.drools.dotnet.examples;

 
rule "We have an honest Politician"
	when
		exists( Politician( Honest == true ) )
	then
		assertLogical( new Hope() );
end;

rule "Hope Lives"
    salience 10
	when
		exists( Hope() )
	then
		System.Console.Out.WriteLine("Hurrah!!! Democracy Lives");
end;

rule "Hope is Dead"
	when
		not( Hope() )
	then
		System.Console.Out.WriteLine( "We are all Doomed!!! Democracy is Dead" );
end;

rule "Currupt the Honest"
   salience 5
	when
		politician : Politician( Honest == true )	
		exists( Hope() )
	then
	    System.Console.Out.WriteLine( "I'm an evil corporation and I have corrupted " + politician.Name );
		politician.Honest =  false ;
		modify( politician );
end;

